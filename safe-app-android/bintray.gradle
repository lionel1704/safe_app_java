Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

def tag = '0.1.0'
def versionId = tag + (project.hasProperty("snapshot") ? "-SNAPSHOT" : "")

def pomConfig = {
  licenses {
    license {
      name 'The 3-Clause BSD License'
      url 'https://opensource.org/licenses/BSD-3-Clause'
    }
    license {
      name 'The MIT License'
      url 'https://opensource.org/licenses/MIT'
    }
  }
  developers {
    developer {
      id "maidsafe"
      name "Maidsafe.net"
      email "qa@maidsafe.net"
    }
  }

  scm {
    url "https://github.com/maidsafe/safe_app_java"
  }
}

publishing {
  publications {
    mavenJava(MavenPublication) {
      artifact("$buildDir/outputs/aar/safe-app-android-${project.hasProperty('mock') ? "mock":"nonMock"}-release-${tag}.aar")
      artifact sourcesJar {
        classifier "sources"
      }
      artifact javadocJar {
        classifier "javadoc"
      }
      groupId 'net.maidsafe'
      artifactId "safe-app-android" + (project.hasProperty('mock') ? "-dev":"")
      version versionId
      pom.withXml {
        def root = asNode()
        root.appendNode('description', 'safe-app library for Android')
        root.appendNode('name', 'Safe App Android' + (project.hasProperty('mock') ? "for Developement":""))
        root.appendNode('url', 'https://github.com/maidsafe/safe_app_java')
        root.children().last() + pomConfig
      }
    }
  }
}

bintray {
  user = properties.getProperty('bintray.user')
  key = properties.getProperty('bintray.apikey')
  publications = ['mavenJava']
  pkg {
    repo = 'newTest'
    name = "safe-app-android" + (project.hasProperty('mock') ? "-dev":"")
    userOrg = 'lionel1704'
    websiteUrl = 'https://docs.maidsafe.net/safe_app_java'
    issueTrackerUrl = 'https://github.com/maidsafe/safe_app_java/issues'
    publicDownloadNumbers = true
    githubRepo = 'maidsafe/safe_app_java'
    githubReleaseNotesFile = 'README.md'
    licenses = ["MIT", "BSD 3-Clause"]
    vcsUrl = 'https://github.com/maidsafe/safe_app_java.git'
    version {
      name = tag
      released  = new Date()
    }
  }
}

task sourcesJar(type: Jar) {
  classifier = 'sources'
  from android.sourceSets.main.java.srcDirs
}

task javadocJar(type: Jar, dependsOn: javadoc) {
  classifier = 'javadoc'
  from javadoc.destinationDir
}

artifacts {
  archives javadocJar
  archives sourcesJar
}
signing {
  sign publishing.publications
}

artifactory {
  contextUrl = 'http://oss.jfrog.org'
  publish {
    repository {
      repoKey = 'oss-snapshot-local'
      username = properties.getProperty('bintray.user')
      password = properties.getProperty('bintray.apikey')
    }
    defaults {
      publications('mavenJava')
      publishArtifacts = true
      publishPom = true
    }
  }
  resolve {
    repoKey = 'jcenter'
  }
}
